#version 460
#extension GL_EXT_ray_tracing : require

// STRUCTS
struct Payload {
    uint seed;

    bool doesScatter;
    vec3 attenuation;
    vec3 scatterDirection;
    vec3 pointOnSphere;
};

struct Ray {
    vec3 origin;
    vec3 direction;
};

struct Camera {
    float fov;
    float aperture;
    float focusDistance;
    vec3 lookFrom;
    vec3 lookAt;
    vec3 up;
};

struct Viewport {
    vec3 horizontal;
    vec3 vertical;
    vec3 upperLeftCorner;
    vec3 cameraUp;
    vec3 cameraRight;
};


// INPUTS
layout(binding = 0, rgba8) uniform image2D renderTarget;
layout(binding = 1) uniform accelerationStructureEXT accelerationStructure;

layout(location = 0) rayPayloadEXT Payload payload;


// CONSTANTS
const float MAX_RAY_COLLISION_DISTANCE = 10000.0f;
const uint MAX_DEPTH = 50;
const uint SAMPLES = 5000;

const Camera camera = Camera(25.0f, 0.0f, 10.0f, vec3(13.0f, 2.0f, -3.0f), vec3(0.0f), vec3(0.0f, 1.0f, 0.0f));


// METHODS
vec3 calculateRayColor(in Ray ray);
Viewport calculateViewport(const float aspectRatio);
Ray getCameraRay(const Viewport viewport, const vec2 uv);
uint getRandomSeed(const uint val0, const uint val1);
float randomFloat();
float randomInInterval(const float min, const float max);


// MAIN
void main() {
    payload.seed = getRandomSeed(gl_LaunchIDEXT.x, gl_LaunchIDEXT.y);

    const vec2 size = vec2(gl_LaunchSizeEXT.xy);
    const float aspectRatio = size.x / size.y;

    const Viewport viewport = calculateViewport(aspectRatio);

    vec3 summedPixelColor = vec3(0.0f);

    for (uint i = 0; i < SAMPLES; i++) {
        const vec2 uv = vec2(gl_LaunchIDEXT.x + randomFloat(), gl_LaunchIDEXT.y + randomFloat()) / size;
        const Ray ray = getCameraRay(viewport, uv);
        summedPixelColor += calculateRayColor(ray);
    }

    const vec3 pixelColor = sqrt(summedPixelColor / SAMPLES);
    imageStore(renderTarget, ivec2(gl_LaunchIDEXT.xy), vec4(pixelColor, 1.0f));
}

// RENDERING
vec3 calculateRayColor(in Ray ray) {
    vec3 reflectedColor = vec3(1.0f);
    vec3 lightSourceColor = vec3(0.0f);

    for (uint depth = 0; depth < MAX_DEPTH; depth++) {
        traceRayEXT(accelerationStructure, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, ray.origin, 0.001f, ray.direction, MAX_RAY_COLLISION_DISTANCE, 0);

        if (payload.doesScatter) {
            reflectedColor *= payload.attenuation;
            ray = Ray(payload.pointOnSphere, normalize(payload.scatterDirection));

        } else {
            // BACKGROUND
            lightSourceColor = payload.attenuation;
            break;
        }
    }

    return reflectedColor * lightSourceColor;
}

// VIEWPORT
Viewport calculateViewport(const float aspectRatio) {
    const float viewportHeight = tan(radians(camera.fov) / 2.0f) * 2.0f;
    const float viewportWidth = aspectRatio * viewportHeight;

    const vec3 cameraForward = normalize(camera.lookAt - camera.lookFrom);
    const vec3 cameraRight = normalize(cross(camera.up, cameraForward));
    const vec3 cameraUp = normalize(cross(cameraForward, cameraRight));

    const vec3 horizontal = viewportWidth * cameraRight * camera.focusDistance;
    const vec3 vertical = viewportHeight * cameraUp * camera.focusDistance;
    const vec3 upperLeftCorner = camera.lookFrom - horizontal / 2.0f + vertical / 2.0f + cameraForward * camera.focusDistance;

    return Viewport(horizontal, vertical, upperLeftCorner, cameraUp, cameraRight);
}

Ray getCameraRay(const Viewport viewport, const vec2 uv) {
    const vec2 random = (camera.aperture / 2.0f) * normalize(vec2(randomInInterval(-1.0f, 1.0f), randomInInterval(-1.0f, 1.0f)));
    const vec3 offset = viewport.cameraRight * random.x + viewport.cameraUp * random.y;

    const vec3 from = camera.lookFrom + offset;
    const vec3 to = viewport.upperLeftCorner + viewport.horizontal * uv.x - viewport.vertical * uv.y;

    return Ray(from, normalize(to - from));
}


// RANDOM
uint getRandomSeed(const uint val0, const uint val1) {
    uint v0 = val0;
    uint v1 = val1;
    uint s0 = 0;

    for (uint n = 0; n < 16; n++) {
        s0 += 0x9e3779b9;
        v0 += ((v1 << 4) + 0xa341316c) ^ (v1 + s0) ^ ((v1 >> 5) + 0xc8013ea4);
        v1 += ((v0 << 4) + 0xad90777d) ^ (v0 + s0) ^ ((v0 >> 5) + 0x7e95761e);
    }

    return v0;
}

uint randomInt() {
    payload.seed = 1664525 * payload.seed + 1013904223;
    return payload.seed;
}

float randomFloat() {
    return float(randomInt() & 0x00FFFFFFu) / float(0x01000000u);
}

float randomInInterval(const float min, const float max) {
    return randomFloat() * (max - min) + min;
}
